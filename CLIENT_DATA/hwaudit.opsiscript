; Copyright (c) uib GmbH (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.

[Actions]
requiredWinstVersion >= "4.11.4.7"
AutoActivityDisplay = true
ScriptErrorMessages=off

DefVar $ProductId$
DefVar $loglevel$
DefVar $logfile$
DefVar $exitcode$
DefVar $timeout$
DefVar $tempdir$
DefVar $taillines$
DefVar $force_install$
DefVar $copy_option$
DefVar $hwaudit$
DefVar $INST_NTVersion$
DefVar $systemtype$

set $tempdir$ = "%Systemdrive%\opsi.org\tmp"
set $ProductId$ = "hwaudit"
set $loglevel$ = getProductProperty("loglevel", "3")
set $logfile$ = getProductProperty("logfile", $tempdir$ + "\hwaudit.log")
Set $timeout$ = GetProductProperty("timeout","")
Set $taillines$ = GetProductProperty("tail-lines", "0")
Set $force_install$ = GetProductProperty("force_install", "false")

set $systemtype$ = GetSystemType

set $INST_NTVersion$ = GetMsVersionInfo

Switch $systemtype$
        Case "x86 System"
                set $hwaudit$ = "hwaudit.exe"
        Endcase
        Case "64 Bit System"
                set $hwaudit$ = "hwaudit-x64.exe"
        Endcase
        DefaultCase
        	comment "Systemtype " + $systemtype$ + " not known. Trying 32Bit for luck"
        	set $hwaudit$ = "hwaudit.exe"
        Endcase
Endswitch

Message "Running hardware audit..."
ShowBitmap "%SCRIPTPATH%\hwaudit.png" "Hardware Inventarisierung"

set $copy_option$ = "-su"
if $force_install$ = "true"
	set $copy_option$ = "-s"
endif

Files_copy_hwaudit

changeDirectory $tempdir$ + "\hwaudit"
comment "$timeout$ = " +  $timeout$
Switch $timeout$
	Case "90"
		Winbatch_hwaudit /WaitForProcessEnding $hwaudit$ /TimeOutSeconds 90
	EndCase
	Case "300"
		Winbatch_hwaudit /WaitForProcessEnding $hwaudit$ /TimeOutSeconds 300
	EndCase
	Case "600"
		Winbatch_hwaudit /WaitForProcessEnding $hwaudit$ /TimeOutSeconds 600
	EndCase
	Case "notimeout"
		Winbatch_hwaudit /WaitForProcessEnding $hwaudit$
	EndCase
	DefaultCase
		comment "default 90"
		Winbatch_hwaudit /WaitForProcessEnding $hwaudit$ /TimeOutSeconds 90
	EndCase
EndSwitch

set $exitcode$ = getLastExitCode
killtask $hwaudit$

if not (FileExists("%SCRIPTPATH%"))
	DosInAnIcon_TryToReconnect
endif 

if fileExists ($logfile$)
	comment "Try to include hwaudit.log file in instlog"
	includelog $logfile$ $taillines$
endif
if not (($exitcode$ = "0") or ($exitcode$ = "259")) 
	logError "Fatal: hwaudit exit code = "+$exitcode$
	isFatalError
endif

[Files_copy_hwaudit]
copy $copy_option$ "%SCRIPTPATH%\files\$hwaudit$" "$tempdir$\hwaudit\"
copy $copy_option$ "%SCRIPTPATH%\common\*.*" "$tempdir$\hwaudit\"
del "$logfile$"

[winbatch_hwaudit]
"$tempdir$\hwaudit\$hwaudit$" -u "%hostId%" -p "%opsiservicePassword%" -a "%opsiserviceURL%" -l $loglevel$ -f "$logfile$"

[DosInAnIcon_TryToReconnect]
net use
set TIMEOUT=
:TRY
if exist "%SCRIPTPATH%\hwaudit.exe" goto READY
%ScriptDrive%
set TIMEOUT=%TIMEOUT%1
if %TIMEOUT% == 1111111111111111 goto READY
"c:\tmp\hwaudit\sleep.exe" 10
net use
goto TRY
:READY

