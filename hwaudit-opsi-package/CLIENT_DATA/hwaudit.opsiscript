; Copyright (c) uib GmbH (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.

[Actions]
requiredWinstVersion >= "4.12.4.17"

DefVar $loglevel$
DefVar $logfile$
DefVar $exitcode$
DefVar $timeout$
DefVar $tempdir$
DefVar $taillines$
DefVar $force_install$
DefVar $copy_option$
DefVar $hw_dir$
DefVar $hw_bin$
DefVar $hw_bin_path$
DefVar $pathdelim$
DefVar $command$

set $tempdir$ = "%opsiTmpDir%"
set $loglevel$ = getProductProperty("loglevel", "5")
Set $timeout$ = GetProductProperty("timeout","300")
Set $taillines$ = GetProductProperty("tail-lines", "10")
Set $force_install$ = GetProductProperty("force_install", "false")

setloglevel = $loglevel$

Message "Running hardware audit..."
ShowBitmap "%SCRIPTPATH%\hwaudit.png" "Hardware Inventarisierung"

Switch GetOs
	Case "Windows_NT"
		set $hw_dir$ = "win"
		set $hw_bin$ = "opsi-hwaudit.exe"
		set $hw_bin_path$ = "%programfiles64dir%\opsi.org\hwaudit\"
		set $pathdelim$ = "\"
		set $command$ = ""
		set $copy_option$ = "-u"
	Endcase

	Case "Linux"
		importlib "uib_lin_install"
		DefVar $result$

		set $hw_dir$ = "lin"
		set $hw_bin$ = "opsi-hwaudit"
		set $hw_bin_path$ = "/usr/local/bin/"
		set $pathdelim$ = "/"
		set $command$ = "chmod 755 "+$hw_bin_path$+$hw_bin$
		set $copy_option$ = ""
		message "Checking for missing Unix-Programs"
		if isOneInstalled(createStringList("lshw")) = "False"
			message "installing lshw"
			set $result$ = linuxInstallOneOf(createStringList("lshw"))
		endif
		if isOneInstalled(createStringList("dmidecode", "dmi-decode")) = "False"
			message "installing dmidecode / dmi-decode"
			set $result$ = linuxInstallOneOf(createStringList("dmidecode", "dmi-decode"))
		endif
		if isOneInstalled(createStringList("pciutils", "lspci")) = "False"
			message "installing pciutils / lspci"
			set $result$ = linuxInstallOneOf(createStringList("pciutils", "lspci"))
		endif
		if isOneInstalled(createStringList("usbutils", "lsusb")) = "False"
			message "installing lsusb / usbutils"
			set $result$ = linuxInstallOneOf(createStringList("usbutils", "lsusb"))
		endif
	Endcase

	Case "macos"
		set $hw_dir$ = "mac"
		set $hw_bin$ = "opsi-hwaudit"
		set $hw_bin_path$ = "/usr/local/bin/"
		set $pathdelim$ = "/"
		set $command$ = "chmod 755 "+$hw_bin_path$+$hw_bin$
		set $copy_option$ = ""
	Endcase
	DefaultCase
		LogError "Unknown OS: "+ $OS$
		isFatalError
	Endcase
Endswitch
set $logfile$ = $tempdir$+$pathdelim$+"hwaudit.log"
set $logfile$ = getProductProperty("logfile", $logfile$)
if ($logfile$ = "") or ($logfile$ = "auto")
	set $logfile$ = $tempdir$+$pathdelim$+"hwaudit.log"
endif

if $force_install$ = "true"
	set $copy_option$ = ""
endif

Files_copy_hwaudit

if not($timeout$  = "notimeout")
	shellInAnIcon_hwaudit /TimeOutSeconds $timeout$
else
	shellInAnIcon_hwaudit
endif
set $exitcode$ = getLastExitCode

if fileExists ($logfile$)
	comment "Try to include hwaudit.log file in instlog"
	includelog $logfile$ $taillines$
endif

if not (($exitcode$ = "0") or ($exitcode$ = "259"))
	logError "Fatal: hwaudit exit code = "+$exitcode$
	isFatalError
endif


[Files_copy_hwaudit]
copy $copy_option$ "%SCRIPTPATH%$pathdelim$files$pathdelim$$hw_dir$$pathdelim$$hw_bin$" "$hw_bin_path$"
$command$
del "$logfile$"

[shellInAnIcon_hwaudit]
echo "$hw_bin_path$$hw_bin$" -u "%opsiserviceUser%" -p "%opsiservicePassword%" -a "%opsiserviceURL%" -l $loglevel$ -f "$logfile$"
"$hw_bin_path$$hw_bin$" -u "%opsiserviceUser%" -p "%opsiservicePassword%" -a "%opsiserviceURL%" -l $loglevel$ -f "$logfile$"
exit $?
