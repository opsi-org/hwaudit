; Copyright (c) uib GmbH (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.

[Actions]
importlib "linuxPackageInstall"

requiredWinstVersion >= "4.12.0"
AutoActivityDisplay = true
ScriptErrorMessages=off

DefVar $ProductId$
DefVar $loglevel$
DefVar $logfile$
DefVar $exitcode$
DefVar $timeout$
DefVar $tempdir$
DefVar $taillines$
DefVar $force_install$
DefVar $copy_option$
DefVar $hw_dir$
DefVar $hw_bin$
DefVar $hw_bin_path$
DefVar $OS$
DefVar $pathdelim$
DefVar $command$
DefVar $dummy$

set $tempdir$ = "%opsiTmpDir%"
set $ProductId$ = "hwaudit"
set $loglevel$ = getProductProperty("loglevel", "5")
Set $timeout$ = GetProductProperty("timeout","")
Set $taillines$ = GetProductProperty("tail-lines", "10")
Set $force_install$ = GetProductProperty("force_install", "false")
set $OS$ = GetOs

setloglevel = $loglevel$

Message "Running hardware audit..."
ShowBitmap "%SCRIPTPATH%\hwaudit.png" "Hardware Inventarisierung"

Switch $OS$
	Case "Windows_NT"
		set $hw_dir$ = "win"
		set $hw_bin$ = "opsi-hwaudit.exe"
		set $hw_bin_path$ = "%programfiles64dir%\opsi.org\hwaudit\"
		set $pathdelim$ = "\"
		set $command$ = ""
		set $copy_option$ = "-u"
	Endcase

	Case "Linux"
		set $hw_dir$ = "lin"
		set $hw_bin$ = "opsi-hwaudit"
		set $hw_bin_path$ = "/usr/local/bin/"
		set $pathdelim$ = "/"
		set $command$ = "chmod 755 "+$hw_bin_path$+$hw_bin$
		set $copy_option$ = ""
		message "Checking for missing Unix-Programs"
		if isAnyInstalled(createStringList("lshw")) = "False"
			message "installing lshw"
			set $dummy$ = linuxInstallAny(createStringList("lshw"))
		endif
		if isAnyInstalled(createStringList("dmidecode", "dmi-decode")) = "False"
			message "installing dmidecode / dmi-decode"
			set $dummy$ = linuxInstallAny(createStringList("dmidecode", "dmi-decode"))
		endif
		if isAnyInstalled(createStringList("lspci", "pciutils")) = "False"
			message "installing lspci"
			set $dummy$ = linuxInstallAny(createStringList("lspci", "pciutils"))
		endif
		if isAnyInstalled(createStringList("lsusb", "usbutils")) = "False"
			message "installing lsusb / usbutils"
			set $dummy$ = linuxInstallAny(createStringList("lsusb", "usbutils"))
		endif
	Endcase

	Case "macos"
		set $hw_dir$ = "macos"
		set $hw_bin$ = "opsi-hwaudit"
		set $hw_bin_path$ = "/usr/local/bin/"
		set $pathdelim$ = "/"
		set $command$ = "chmod 755 "+$hw_bin_path$+$hw_bin$
		set $copy_option$ = ""
	Endcase
	DefaultCase
		LogError "Unknown OS: "+ $OS$
		isFatalError
	Endcase
Endswitch
set $logfile$ = $tempdir$+$pathdelim$+"hwaudit.log"
set $logfile$ = getProductProperty("logfile", $logfile$)
if ($logfile$ = "") or ($logfile$ = "auto")
	set $logfile$ = $tempdir$+$pathdelim$+"hwaudit.log"
endif

if $force_install$ = "true"
	set $copy_option$ = ""
endif

Files_copy_hwaudit

if not($timeout$  = "notimeout")
	Winbatch_hwaudit /TimeOutSeconds $timeout$
else
	Winbatch_hwaudit
endif
set $exitcode$ = getLastExitCode
killtask $hw_bin$

if not (($exitcode$ = "0") or ($exitcode$ = "259")) 
	comment "retry ..."
	shellInAnIcon_hwaudit
	set $exitcode$ = getLastExitCode
	killtask $hw_bin$
endif

if fileExists ($logfile$)
	comment "Try to include hwaudit.log file in instlog"
	includelog $logfile$ $taillines$
endif


if not (($exitcode$ = "0") or ($exitcode$ = "259")) 
	logError "Fatal: hwaudit exit code = "+$exitcode$
	isFatalError
endif

[Files_copy_hwaudit]
copy $copy_option$ "%SCRIPTPATH%$pathdelim$files$pathdelim$$hw_dir$$pathdelim$$hw_bin$" "$hw_bin_path$"
$command$
del "$logfile$"

[winbatch_hwaudit]
"$hw_bin_path$$hw_bin$" -u "%opsiserviceUser%" -p "%opsiservicePassword%" -a "%opsiserviceURL%" -l $loglevel$ -f "$logfile$"

[shellInAnIcon_hwaudit]
"$hw_bin_path$$hw_bin$" -u "%opsiserviceUser%" -p "%opsiservicePassword%" -a "%opsiserviceURL%" -l $loglevel$ -f "$logfile$"
